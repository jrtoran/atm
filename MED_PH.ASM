;		RUTINA MED_PH
; PIND, 2 SELECIONA SUBIR O BAJAR PH SIN PUENTE BAJAR

PRO2:	; PARA MEDIR EL PH
	WDR
	; VALOR DE MEDICION TOTAL	
	LDI				R18, 02				; LINEA DE ENTRADA EN EL CONVERTIDOR PCF8591
	LDI				R26,180				; NUMERO DE LECTURAS Ó CALIBRACION
	RCALL			PCF


	LDI				R16, 15				; VALOR MINIMO NO SE DETECTA SONDA DE Ph
	CP				R24, R16
	BRSH			PRO2D				; SALTA PRO5D SI EL MODULO ESTA CONECTADO
	CBR			CONTROL, 0B00010000		; LINEA DE CONTROL SIN SONDA DE Ph SOLO PRESENTA LINEA DE PRODUCION Y SAL
	RET
; TIENE QUE SER EL VALOR LEIDO + Ó - DOS UNIDADES PARA QUE SE GRABE EN LA RAM


PRO2D:
	SBR			CONTROL, 0B00010000		; LINEA DE CONTROL 
	LDI				R16, 54 + 10		; RESTA 10 PARA LUEGO SUMERLE LA CALIBRACION ENTRE 0 Y 20
	SUB				R24, R16			; Y RESTANDOLO DEL LEIDO			
	MOV				R16, R24

	LDS				R19, RAMC_PH		; LEE EL VALOR DE CALIBRACION Y
	ADD				R16, R19			; SE LA SUMA AL LEIDO
	STS 			PH_REAL, R16
	LDS				R24, RAM_PH			; LEE EL VALOR GRABADO EN RAM Y AMORTIGUADO
	ADIW			R24, 2				; INCREMENTO EN DOS UNIDADES Y
	CP				R16, R24			; LO COMPARA CON EL VALOR ULTIMO LEIDO
	BRLT			PRO2D1				; SI NO ES MAYOR DE DOS DECIMAS NO LO ALMACENA
	DEC				R24
	STS				RAM_PH, R24			; ALMACENA LA MEDICION EN LA MEMORIA RAM
	RJMP			PRO2D2
PRO2D1:
	SUBI			R24, 4
	CP				R24,R16				; Y LO VUELVE A COMPARAR CON EL ALMACENADO
	BRLT			PRO2D2
	INC				R24
	STS				RAM_PH, R24			; ALMACENA LA MEDICION EN LA MEMORIA RAM
PRO2D2:
	LDS				R19, RAM_PH			
	LDS				R18, RAMP_PH
	; APAGA BOMBA DE PH SI EL VALOR LEIDO ES MAYOR DE 9.0 POSIBLE AVERIA APAGA BOMBA

	LDI				R24,91				; VALOR MAX. DEL PH PARA APAGAR BOMBA DOSIFICADORA DE PH
	CP				R19,R24				; SI R19 ES MENOR QUE R24 VA A PRO2A
	BRLT			PRO2E1				; SI NO ES MAYOR R19 QUE R24 NO SALTA A PRO2E
	MOV				R16, VAL_LED
	ANDI			R16, 0B11101111		; APAGA LED 4 PH
	MOV				VAL_LED, R16
	CBI				PORTD,b_acido		; APAGA BOMBA DE ACIDO
	RET

PRO2E1:

;APAGAR BOMBA DE PH DESPUES DE X MINUTOS/hora
	LDS  			R25, minu			; CARGA LOS MINUTOS BOMBA ENCENDIDA
	LDI				R18, 30				; CARGA EL TIEMPO DE FUNCIONAMIENTO DE LA BOMBA
	RCALL			COMP				; LO COMPARA Y SI ES MAYOR APAGA LA BOMBA DE PH 
	SBRC			CONTROL, 0
	RJMP			PRO2E
	MOV				R16, VAL_LED
	ANDI			R16, 0B11101111		; APAGA LED 4 PH
	MOV				VAL_LED, R16
	CBI				PORTD,b_acido		; APAGA BOMBA DE ACIDO
	RET


PRO2E:
	; ENCIENDE BOMBA DE PH SI EL VALOR LEIDO ES MAYOR QUE EL PROGRAMADO
;	SBIC			DDRB, 1				; COMPRUEBA QUE LA FUENTE ESTA APAGADA EN CUYO CASO APAGA LA BOMBA DE PH
;	RJMP			PRO2F1
	SBRC			VAL_LED, 5			; VERIFICA EL LED 5 FALTA DE NIVEL EN EL DEPOSITO
	RJMP			PRO2F1				; Y SALTA ESTA INSTRUCION SI ES UNO "HAY NIVEL"

	LDS				R18, RAMP_PH
	INC				R18
	SBRC			CONTROL_1, 1		; salta la siguiente instrucion si es UNO
	CP				R18,R19				; SI EL PH MEDIDO ES MAYOR QUE EL PROGRAMADO
	SBRS			CONTROL_1, 1		; salta la siguiente instrucion si es CERO
	CP				R19,R18				; SI EL PH MEDIDO ES MENOR QUE EL PROGRAMADO
	BRLT			PRO2F				; SALTA A PRO2A (OP1 > OP2 SALTA)
	MOV				R16, VAL_LED
	ORI				R16, 0B00010000		; ENCIENDE LED 4 PH EN PANTALLA
	MOV				VAL_LED, R16	
	SBI				PORTD,b_acido		; ENCIENDE BOMBA DE ACIDO
	RET
PRO2F: ;APAGA EL PH  SI EL VALOR LEIDO ES MENOR QUE EL PROGRAMADO
	DEC				R18
	DEC				R18
	SBRC			CONTROL_1, 1		; salta la siguiente instrucion si es UNO
	CP				R19,R18				; PARA SUBIR EL PH
	SBRS			CONTROL_1, 1		; salta la siguiente instrucion si es CERO
	CP				R18,R19				; PARA BAJAR EL PH
	BRLT			PRO2G
PRO2F1:
	MOV				R16, VAL_LED
	ANDI			R16, 0B11101111		; APAGA LED 4 PH EN PANTALLA
	MOV				VAL_LED, R16
	CBI				PORTD,b_acido		; APAGA BOMBA DE ACIDO
PRO2G:
;	CBR				CONTROL,0B00000010	; PONE A CERO EL BIT 1
	RET
